`/mnt/c/Users/mpaulus/source/repos/genopt-xlim/gen_hx_part_load.tsv`
r/Gen HX Customer Flow/ V_r {gpm}
r/Gen HX Customer Inlet Temp/ T_5_data {°F}
r/Gen HX Customer Leaving Temp/ T_1_data {°F}
r/Part Load/ X_g {%}
r/Gen Elec Efficiency/ η_e {%}

T_5_data_R = R:T_5_data;
T_1_data_R = R:T_1_data;

-- Trying to account for slightly lower density for the higher water temperatures in the reclaim loop.
-- 180°F is kinda around average of the relevant temperatures in the HX.
water_density_@_180_degF = 970 {kg/m^3}

s_w = 1 {Btu/lbm °R} * water_density_@_180_degF

η_e: plr {%} = -0.00096 {1/%} * plr^2 + 0.212 * plr + 32.2 {%}-- This is a curve fit to the part load data from Mustang CAT.

E_g {kW} = X_g * 2490 {kW}
Q_g_r {kBTU/h} = V_r * s_w * (T_1 - T_5_data)
Q_g_i {kBTU/h} = E_g / η_e


T_6 = a * X_g^2 + X_g * b + c -- T_6 is the inlet temperature to the HX on the generator side, assuming a quadratic relationship to the electrical part load.

η_r {%} = Q_g_r / Q_g_i
η_t = η_r + η_e

a = 0.0002 {°R/%^2}
b = 0.11 {°R/%}
c = 676.125 {°R}
UA_g = 355.625 {kBtu/hr °R}
V_g = 547.375 {gpm}

C_g {btu/h °F} = V_g * s_w
C_r {Btu/h °F} = V_r * s_w

C_min = if C_g < C_r then C_g else C_r
C_max = if C_g > C_r then C_g else C_r

NTU {} = UA_g / C_min
R_C {} = C_min / C_max

-- This is counterflow effectiveness formula, rearranged a little
ε_g {} = (C_g * exp: ((C_r - C_g) * UA_g) / (C_g * C_r); - C_g) / (C_r * exp: ((C_r - C_g) * UA_g) / (C_g * C_r); - C_g)
T_1 = (T_6 - R:T_5_data;) * ε_g + R:T_5_data; -- T_1 is the leaving temperature of the customer water
T_7 = (C_g * T_6 + C_r * T_5_data_R - C_r * T_1) / C_g -- T_7 is the leaving temperature of the generator side water

T_1_diff_sq = (T_1_data_R - T_1)^2
sse = sum: T_1_diff_sq;
